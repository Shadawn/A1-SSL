#Область СведенияОВнешнейОбработке

#Если НЕ Клиент Тогда
	
	// Используется для создания структуры, которую возвращает функция СведенияОВнешнейОбработке(), вызываемая БСП.
	//
	// Параметры:
	//  ОбработкаОбъект		 - ОбработкаОбъект - получается через ЭтотОбъект. Из него извлекается имя и представление.
	//  ОсновныеПараметры	 - Структура - может содержать ключи:
	//    Вид - Строка - одно из:
	//      "ПечатнаяФорма", "ЗаполнениеОбъекта", "СозданиеСвязанныхОбъектов", "Отчет", "ШаблонСообщения", "ДополнительнаяОбработка", "ДополнительныйОтчет".
	//    Версия - Строка - 
	//    БезопасныйРежим - Булево - определяет, требуется ли выполнение в безопасном режиме.
	//    Назначение - Строка,МассивСтрок - список полных имен объектов метаданных для подключения в виде массива или строки, разделенной запятыми.
	//    Использование - Строка - использование команд обработки по умолчанию. Одно из:
	//      ВызовКлиентскогоМетода	
	//    
	// 
	// Возвращаемое значение:
	//   - 
	//
	Функция СведенияОВнешнейОбработке(ОбработкаОбъект, ОсновныеПараметры) Экспорт
		ПолныеОсновныеПараметры = ПолныеОсновныеПараметры(ОбработкаОбъект, ОсновныеПараметры);
		
		ПараметрыРегистрации = Новый Структура;
		
		ПараметрыРегистрации.Вставить("Вид", ПолныеОсновныеПараметры.Вид);  
		ПараметрыРегистрации.Вставить("Наименование", ПолныеОсновныеПараметры.Наименование);
		ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
		ПараметрыРегистрации.Вставить("Версия", ПолныеОсновныеПараметры.Версия); 
		ПараметрыРегистрации.Вставить("Информация", "Дополнительная обработка """ + ПолныеОсновныеПараметры.Наименование +"""."); 
		
		ТаблицаКоманд = ПолучитьТаблицуКоманд();
		Если НЕ ПолныеОсновныеПараметры.Свойство("Команды") Тогда 
			ДобавитьКоманду(ТаблицаКоманд, ПолныеОсновныеПараметры.Наименование, ПолныеОсновныеПараметры.Идентификатор,
			ПолныеОсновныеПараметры.Использование, Истина, ПолныеОсновныеПараметры.Модификатор);
		Иначе
			Для Каждого ОписаниеКоманды Из ПолныеОсновныеПараметры.Команды Цикл
				Команда = ДобавитьКоманду(ТаблицаКоманд, ПолныеОсновныеПараметры.Наименование, ПолныеОсновныеПараметры.Идентификатор,
				ПолныеОсновныеПараметры.Использование, Истина, ПолныеОсновныеПараметры.Модификатор);
				ЗаполнитьЗначенияСвойств(Команда, ОписаниеКоманды);
			КонецЦикла;
		КонецЕсли;
		
		ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
		
		Если ПолныеОсновныеПараметры.Свойство("Назначение") Тогда
			ПараметрыРегистрации.Вставить("Назначение", А1Э_Массивы.Массив(ПолныеОсновныеПараметры.Назначение));
		КонецЕсли;
		Возврат ПараметрыРегистрации;
	КонецФункции
	
	Функция ПолныеОсновныеПараметры(ОбработкаОбъект, ОсновныеПараметры)
		ПолныеОсновныеПараметры = А1Э_Структуры.Скопировать(ОсновныеПараметры);
		ПолныеОсновныеПараметры.Вставить("Идентификатор", ОбработкаОбъект.Метаданные().Имя);
		ПолныеОсновныеПараметры.Вставить("Наименование", ОбработкаОбъект.Метаданные().Синоним);
		
		Если НЕ ПолныеОсновныеПараметры.Свойство("Версия") Тогда
			ПолныеОсновныеПараметры.Вставить("Версия", "1.0.0");
		КонецЕсли;
		Если НЕ ПолныеОсновныеПараметры.Свойство("Информация") Тогда
			ПолныеОсновныеПараметры.Вставить("Информация", "Дополнительная обработка """ + ПолныеОсновныеПараметры.Наименование +"""."); 
		КонецЕсли;
		Если НЕ ПолныеОсновныеПараметры.Свойство("Использование") Тогда
			ПолныеОсновныеПараметры.Вставить("Использование", "ВызовСерверногоМетода");
		КонецЕсли;
				
		Если ОсновныеПараметры.Вид = "ПечатьMXL" Тогда
			ПолныеОсновныеПараметры.Вставить("Модификатор", ОсновныеПараметры.Вид);
			ПолныеОсновныеПараметры.Вид = "ПечатнаяФорма";
		Иначе
			ПолныеОсновныеПараметры.Вставить("Модификатор", "");
		КонецЕсли;

		Возврат ПолныеОсновныеПараметры;
	КонецФункции
	
	Функция ПолучитьТаблицуКоманд()
		Команды = Новый ТаблицаЗначений;
		Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));//как будет выглядеть описание печ.формы для пользователя
		Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка")); //имя макета печ.формы
		Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка")); //ВызовСерверногоМетода
		Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
		Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
		Возврат Команды;
	КонецФункции
	
	Функция ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
		
		НоваяКоманда = ТаблицаКоманд.Добавить();
		НоваяКоманда.Представление = Представление; 
		НоваяКоманда.Идентификатор = Идентификатор;
		НоваяКоманда.Использование = Использование;
		НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
		НоваяКоманда.Модификатор = Модификатор;
		Возврат НоваяКоманда; 
	КонецФункции
	
#КонецЕсли

#Область РекомендуемоеИспользование
	//Функция ОсновныеПараметры()
	//	ОсновныеПараметры = Новый Структура;
	//	ОсновныеПараметры.Вставить("Вид", "ПечатнаяФорма");
	//	ОсновныеПараметры.Вставить("Модификатор", "ПечатьMXL");
	//	ОсновнаеПараметры.Вставить()
	//	Возврат ОсновныеПараметры;
	//КонецФункции 
	//
	//Функция СведенияОВнешнейОбработке() Экспорт
	//	ПараметрыРегистрации = А1Э_БСП.СведенияОВнешнейОбработке(ЭтотОбъект, ОсновныеПараметры());
	//	
	//	Возврат ПараметрыРегистрации;
	//КонецФункции 
#КонецОбласти

#КонецОбласти 
