Функция НастройкиМеханизма() Экспорт
	Настройки = А1Э_Механизмы.НовыйНастройкиМеханизма();
	
	Настройки.Обработчики.Вставить("А1Емэйл_ПриОпределенииПолучателей", Истина);
	Настройки.Обработчики.Вставить("А1Э_ПриОпределенииТекущегоПользователя", Истина);
	Настройки.Обработчики.Вставить("А1Э_ПриОпределенииПользователяИБ", Истина);
	
	Возврат Настройки;
КонецФункции

#Если НЕ Клиент Тогда
	
	Функция А1Емэйл_ПриОпределенииПолучателей(МассивСсылок, Получатели) Экспорт
		СоответствиеТипов = А1Э_Массивы.РазбитьПоТипам(МассивСсылок);
		МассивЧастейЗапроса = Новый Массив;
		ШаблонЗапроса = 
		"ВЫБРАТЬ
		|	ТЧКонтактнаяИнформация.АдресЭП КАК АдресЭП
		|ИЗ
		|	Документ___КонтактнаяИнформация КАК ТЧКонтактнаяИнформация
		|ГДЕ
		|	ТЧКонтактнаяИнформация.Ссылка В(&МассивСсылок)
		|	И ТЧКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)";
		Для Каждого Пара Из СоответствиеТипов Цикл
			ОбъектМетаданных = А1Э_Метаданные.ОбъектМетаданных(Пара.Ключ);
			Если ОбъектМетаданных.ТабличныеЧасти.Найти("КонтактнаяИнформация") = Неопределено Тогда Продолжить; КонецЕсли;
			МассивЧастейЗапроса.Добавить(СтрЗаменить(ШаблонЗапроса, "Документ___КонтактнаяИнформация", ОбъектМетаданных.ПолноеИмя() + ".КонтактнаяИнформация"));
		КонецЦикла;
		Если МассивЧастейЗапроса.Количество() = 0 Тогда Возврат Неопределено; КонецЕсли;
		Запрос = Новый Запрос;
		Запрос.Текст = А1Э_Запросы.Объединить(МассивЧастейЗапроса);
		Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Получатели.Добавить(Выборка.АдресЭП);
		КонецЦикла;
		
	КонецФункции
	
	Функция А1Э_ПриОпределенииТекущегоПользователя(ТекущийПользователь) Экспорт
		ТекущийПользователь = Пользователи.ТекущийПользователь();
	КонецФункции
	
	Функция А1Э_ПриОпределенииПользователяИБ(ПользовательИБ, ИдентификаторПользователя) Экспорт
		Если ТипЗнч(ИдентификаторПользователя) = Тип("СправочникСсылка.Пользователи") Тогда
			ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ИдентификаторПользователя.ИдентификаторПользователяИБ);
		КонецЕсли;
	КонецФункции 

#КонецЕсли

