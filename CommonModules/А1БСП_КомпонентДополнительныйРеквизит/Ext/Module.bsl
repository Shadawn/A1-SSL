#Если НЕ Клиент Тогда
	
	Функция ДобавитьОписание(МассивОписаний, ИмяКомпонента, РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт
		ИмяРеквизита = А1Э_Структуры.ЗначениеСвойства(Параметры, "ИмяРеквизита", ИмяКомпонента);
		Реквизит = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", ИмяРеквизита);
		Если НЕ ЗначениеЗаполнено(Реквизит) Тогда
			А1Э_Служебный.СлужебноеИсключение("При создании компонента <" + ИмяКомпонента + "> не удалось найти доп. реквизит с именем <" + ИмяРеквизита + ">");
		КонецЕсли;
		
		РабочиеПараметры = А1Э_Структуры.Скопировать(Параметры);
		Если Реквизит.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Или Реквизит.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) Тогда
			РабочиеПараметры.Вставить("ПараметрыВыбора", А1Э_Формы.НовыйПараметрыВыбора(
			"Отбор.Владелец", Реквизит,
			));
		КонецЕсли;
		РабочиеПараметры.Вставить("ХранитьНачальноеЗначение", Истина);
		РабочиеПараметры.Вставить("ИмяЭлемента", ИмяКомпонента + "___Поле");
		
		СвойстваГруппы = А1Э_Массивы.Создать("Видимость", "Доступность");
		ПараметрыГруппы = Новый Структура;
		Для Каждого ИмяСвойства Из СвойстваГруппы Цикл
			Если НЕ РабочиеПараметры.Свойство(ИмяСвойства) Тогда Продолжить; КонецЕсли;
			
			ПараметрыГруппы.Вставить(ИмяСвойства, РабочиеПараметры[ИмяСвойства]);
			РабочиеПараметры.Удалить(ИмяСвойства);	
		КонецЦикла;
		
		РабочиеДействия = А1Э_Структуры.Скопировать(Действия);
		А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействия, ИмяМодуля(), "ПриИзменении");
		
		А1Э_УниверсальнаяФорма.ДобавитьОписаниеНастроекКомпонента(МассивОписаний, ИмяКомпонента, 
		А1Э_Структуры.Создать(
		"ИмяРеквизита", ИмяРеквизита,
		"ЭтоДополнительноеСведение", Реквизит.ЭтоДополнительноеСведение,
		));
		А1Э_Формы.ДобавитьОписаниеГоризонтальнойГруппы(МассивОписаний, ИмяКомпонента, , РодительЭлемента, ПередЭлементом, ПараметрыГруппы, А1Э_Структуры.Создать(
		"ФормаПриСозданииНаСервере", ИмяМодуля() + ".ФормаПриСозданииНаСервере",
		"ФормаПередЗаписьюНаСервере", ИмяМодуля() + ".ФормаПередЗаписьюНаСервере",
		"ФормаПриЗаписиНаСервере", ИмяМодуля() + ".ФормаПриЗаписиНаСервере",
		));
		А1Э_Формы.ДобавитьОписаниеРеквизитаИЭлемента(МассивОписаний, ИмяКомпонента, Реквизит.ТипЗначения, , Реквизит.Заголовок, ИмяКомпонента, , РабочиеПараметры, РабочиеДействия);
		Если Реквизит.ЭтоДополнительноеСведение Тогда
			А1Э_Формы.ДобавитьОписаниеКомандыИКнопки2(МассивОписаний, ИмяКомпонента + "___Сохранить", А1Э_Массивы.Создать(
			"А1Э_УниверсальнаяФорма.ЗаписатьЕслиНовый:НаКлиентеАсинхронно",
			ИмяМодуля() + ".Сохранить:НаСервере",
			), , ИмяКомпонента, , А1Э_Структуры.Создать(
			"Отображение", ОтображениеКнопки.Картинка,
			"Картинка", БиблиотекаКартинок.СохранитьФайл,
			));
		КонецЕсли;
	КонецФункции
	
	Функция ФормаПриСозданииНаСервере(ИмяКомпонента, Форма, Отказ, СтандартнаяОбработка) Экспорт 
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		Форма[ИмяКомпонента] = А1Э_ДопРеквизиты.Значение(Форма.Объект, Настройки.ИмяРеквизита, Настройки.ЭтоДополнительноеСведение);
		А1Э_Формы.СинхронизироватьРеквизит(Форма, ИмяКомпонента);
	КонецФункции
	
	Функция ФормаПередЗаписьюНаСервере(ИмяКомпонента, Форма, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт
		Если НЕ ТекущийОбъект.ДополнительныеСвойства.Свойство("А1БСП_КомпонентДополнительныйРеквизит__Значения") Тогда
			ТекущийОбъект.ДополнительныеСвойства.Вставить("А1БСП_КомпонентДополнительныйРеквизит__Значения", Новый Структура);
		КонецЕсли;

		Если Форма[ИмяКомпонента] = А1Э_Формы.НачальноеЗначениеРеквизита(Форма, ИмяКомпонента) Тогда Возврат Неопределено; КонецЕсли;
		
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		Если Настройки.ЭтоДополнительноеСведение = Ложь Тогда
			//Для доп. реквизитов устанавливаем их до записи.
			А1Э_ДопРеквизиты.Установить(ТекущийОбъект, Настройки.ИмяРеквизита, Форма[ИмяКомпонента], Настройки.ЭтоДополнительноеСведение);
		Иначе
			//Доп. сведения запоминаем и устанавливаем после, иначе возникают ошибки при записи новых.
			ТекущийОбъект.ДополнительныеСвойства.А1БСП_КомпонентДополнительныйРеквизит__Значения.Вставить(Настройки.ИмяРеквизита, Форма[ИмяКомпонента]);	
		КонецЕсли;
		
	КонецФункции
	
	Функция ФормаПриЗаписиНаСервере(ИмяКомпонента, Форма, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		Если Настройки.ЭтоДополнительноеСведение = Ложь Тогда
			//Предполагаем, что доп. реквизит мог измениться при записи объекта.
			Форма[ИмяКомпонента] = А1Э_ДопРеквизиты.Значение(ТекущийОбъект, Настройки.ИмяРеквизита, Настройки.ЭтоДополнительноеСведение);
		Иначе
			//Предполагаем, что доп. сведения не изменяются в процессе.
			НовоеЗначение = А1Э_Структуры.ЗначениеСвойства(ТекущийОбъект.ДополнительныеСвойства.А1БСП_КомпонентДополнительныйРеквизит__Значения, Настройки.ИмяРеквизита, Null);
			Если НовоеЗначение <> Null Тогда
				А1Э_ДопРеквизиты.Установить(ТекущийОбъект, Настройки.ИмяРеквизита, НовоеЗначение, Настройки.ЭтоДополнительноеСведение);	
			КонецЕсли;
			А1Э_Формы.СинхронизироватьРеквизит(Форма, ИмяКомпонента);
		КонецЕсли;
	КонецФункции
	
	Функция Сохранить(Форма, ИмяКоманды) Экспорт
		ИмяКомпонента = А1Э_УниверсальнаяФорма.ИмяКомпонента(ИмяКоманды);
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		Если Форма[ИмяКомпонента] = А1Э_Формы.НачальноеЗначениеРеквизита(Форма, ИмяКомпонента) Тогда Возврат Неопределено; КонецЕсли;
		А1Э_ДопРеквизиты.Установить(Форма.Объект.Ссылка, Настройки.ИмяРеквизита, Форма[ИмяКомпонента], Настройки.ЭтоДополнительноеСведение);
		А1Э_Формы.СинхронизироватьРеквизит(Форма, ИмяКомпонента);
	КонецФункции
	
#КонецЕсли
#Если Клиент Тогда
	
	Функция ПриИзменении(Форма, Элемент) Экспорт
		Форма.Модифицированность = Истина;
	КонецФункции
	
#КонецЕсли
 

Функция ИмяМодуля() Экспорт 
	Возврат "А1БСП_КомпонентДополнительныйРеквизит";
КонецФункции 
