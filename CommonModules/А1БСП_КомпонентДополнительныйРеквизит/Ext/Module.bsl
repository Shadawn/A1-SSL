#Если НЕ Клиент Тогда
	
	Функция ДобавитьОписание(МассивОписаний, ИмяКомпонента, РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт
		ИмяРеквизита = А1Э_Структуры.ЗначениеСвойства(Параметры, "ИмяРеквизита", ИмяКомпонента);
		Реквизит = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", ИмяРеквизита);
		Если НЕ ЗначениеЗаполнено(Реквизит) Тогда
			А1Э_Служебный.СлужебноеИсключение("При создании компонента <" + ИмяКомпонента + "> не удалось найти доп. реквизит с именем <" + ИмяРеквизита + ">");
		КонецЕсли;
		
		РабочиеПараметры = А1Э_Структуры.Скопировать(Параметры);
		Если Реквизит.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Или Реквизит.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) Тогда
			РабочиеПараметры.Вставить("ПараметрыВыбора", А1Э_Формы.НовыйПараметрыВыбора(
			"Отбор.Владелец", Реквизит,
			));
		КонецЕсли;
		РабочиеПараметры.Вставить("ХранитьНачальноеЗначение", Истина);
		
		РабочиеДействия = А1Э_Структуры.Скопировать(Действия);
		А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействия, ИмяМодуля(), "ФормаПриСозданииНаСервере", 0);
		А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействия, ИмяМодуля(), "ФормаПередЗаписьюНаСервере", 0);
		А1Э_УниверсальнаяФорма.ДобавитьОбработчикМодуляКДействиям(РабочиеДействия, ИмяМодуля(), "ФормаПриЗаписиНаСервере", 0);
		
		А1Э_УниверсальнаяФорма.ДобавитьОписаниеНастроекКомпонента(МассивОписаний, ИмяКомпонента, 
		А1Э_Структуры.Создать(
		"ИмяРеквизита", ИмяРеквизита,
		"ЭтоДополнительноеСведение", Реквизит.ЭтоДополнительноеСведение,
		));
		А1Э_Формы.ДобавитьОписаниеРеквизитаИЭлемента(МассивОписаний, ИмяКомпонента, Реквизит.ТипЗначения, , Реквизит.Заголовок, РодительЭлемента, ПередЭлементом, РабочиеПараметры, РабочиеДействия);
	КонецФункции
	
	Функция ФормаПриСозданииНаСервере(ИмяКомпонента, Форма, Отказ, СтандартнаяОбработка) Экспорт 
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		Форма[ИмяКомпонента] = А1Э_ДопРеквизиты.Значение(Форма.Объект, Настройки.ИмяРеквизита, Настройки.ЭтоДополнительноеСведение);
		А1Э_Формы.СинхронизироватьРеквизит(Форма, ИмяКомпонента);
	КонецФункции
	
	Функция ФормаПередЗаписьюНаСервере(ИмяКомпонента, Форма, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт
		Если Форма[ИмяКомпонента] = А1Э_Формы.НачальноеЗначениеРеквизита(Форма, ИмяКомпонента) Тогда Возврат Неопределено; КонецЕсли;
		
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		А1Э_ДопРеквизиты.Установить(ТекущийОбъект, Настройки.ИмяРеквизита, Форма[ИмяКомпонента], Настройки.ЭтоДополнительноеСведение);
	КонецФункции
	
	Функция ФормаПриЗаписиНаСервере(ИмяКомпонента, Форма, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		Форма[ИмяКомпонента] = А1Э_ДопРеквизиты.Значение(ТекущийОбъект, Настройки.ИмяРеквизита, Настройки.ЭтоДополнительноеСведение);
	КонецФункции
	
#КонецЕсли


Функция ИмяМодуля() Экспорт 
	Возврат "А1БСП_КомпонентДополнительныйРеквизит";
КонецФункции 
