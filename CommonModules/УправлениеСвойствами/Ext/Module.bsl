
&После("СоздатьОсновныеОбъектыФормы")
Процедура А1БСП_СоздатьОсновныеОбъектыФормы(Форма, Контекст, СоздатьОписаниеДополнительныхРеквизитов)
	Попытка //Добавляем кнопку для добавления полей к самим полям
		ОпцияИспользоватьСвойства = Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьДополнительныеРеквизитыИСведения");
		ДобавитьКомандуСоздания = ОпцияИспользоватьСвойства И СоздатьОписаниеДополнительныхРеквизитов;
		МассивОписаний = Новый Массив;
		Если ДобавитьКомандуСоздания Тогда
			А1Э_Формы.ДобавитьОписаниеКомандыИКнопки(МассивОписаний, "А1БСП_ДобавитьРеквизит", "Подключаемый_СвойстваВыполнитьКоманду", , "Добавить новое поле", Форма.Свойства_ИмяЭлементаДляРазмещения, , ,
			А1Э_Структуры.Создать(
			"Картинка", БиблиотекаКартинок.СоздатьЭлементСписка,
			"Отображение", ОтображениеКнопки.КартинкаИТекст, 
			)); 
		КонецЕсли;
		А1Э_Формы.ДобавитьРеквизитыИЭлементы(Форма, МассивОписаний);
		
		Если ОпцияИспользоватьСвойства И СоздатьОписаниеДополнительныхРеквизитов Тогда
			Если НЕ ПравоДоступа("Изменение", Метаданные.Справочники.НаборыДополнительныхРеквизитовИСведений) Тогда
				Форма.Элементы.А1БСП_ДобавитьРеквизит.Доступность = Ложь;
				Форма.Элементы.А1БСП_ДобавитьРеквизит.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
				Форма.Команды.А1БСП_ДобавитьРеквизит.Подсказка = "У Вас недостаточно прав для добавления полей. Обратитесь к администратору."; 
			КонецЕсли;
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
	КонецПопытки;
	
	Если А1Э_УниверсальнаяФорма.Инициализирована(Форма) Тогда //Для универсальной формы используем универсальные обработчики. 
		Для Каждого Команда Из Форма.Команды Цикл
			Если Команда.Действие = "Подключаемый_СвойстваВыполнитьКоманду" Тогда
				А1Э_УниверсальнаяФорма.ЗаменитьДействиеКомандыФормы(Форма, Команда, "А1БСП_ДополнительныеРеквизиты.Подключаемый_СвойстваВыполнитьКоманду");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры

&После("ЗаполнитьДополнительныеРеквизитыВФорме")
Процедура А1БСП_ЗаполнитьДополнительныеРеквизитыВФорме(Форма, Объект, ПоляНадписей, СкрытьУдаленные)
	Если НЕ А1Э_УниверсальнаяФорма.Инициализирована(Форма) Тогда Возврат; КонецЕсли;
	
	//Реализация через универсальные обработчики.
	Для каждого ОписаниеСвойства Из Форма.Свойства_ОписаниеДополнительныхРеквизитов Цикл
		Элемент = Форма.Элементы[ОписаниеСвойства.ИмяРеквизитаЗначение];
		Если ОписаниеСвойства.СтрокаСсылочногоТипа Тогда
			А1Э_УниверсальнаяФорма.ДобавитьОбработчикЭлементаФормы(Форма, Элемент, "ОбработкаНавигационнойСсылки", "А1БСП_ДополнительныеРеквизиты.Подключаемый_СвойстваВыполнитьКоманду");
		КонецЕсли;
		А1Э_УниверсальнаяФорма.ДобавитьОбработчикЭлементаФормы(Форма, Элемент, "ПриИзменении", "А1БСП_ДополнительныеРеквизиты.Подключаемый_ПриИзмененииДополнительногоРеквизита");
	КонецЦикла;
КонецПроцедуры

&Вместо("ИспользуютсяСвойства")
Функция А1БСП_ИспользуютсяСвойства(Форма, ДополнительныеПараметры)
	Если А1Э_Механизмы.Подключен(Форма, "А1БСП_ДополнительныеРеквизиты") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Результат = ПродолжитьВызов(Форма, ДополнительныеПараметры);
	Возврат Результат;
КонецФункции

