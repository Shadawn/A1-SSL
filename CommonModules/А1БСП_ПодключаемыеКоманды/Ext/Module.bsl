Функция НастройкиМеханизма() Экспорт
	Настройки = А1Э_Механизмы.НовыйНастройкиМеханизма();
	
	Настройки.Обработчики.Вставить("ФормаПриЧтенииНаСервере", Истина);
	Настройки.Обработчики.Вставить("ФормаПриСозданииНаСервере", Истина);
	Настройки.Обработчики.Вставить("ФормаПриОткрытии", Истина);
	
	Возврат Настройки;
КонецФункции

#Область События
#Если НЕ Клиент Тогда
	
	Функция ФормаПриЧтенииНаСервере(Форма, ТекущийОбъект) Экспорт 
		Если А1Э_Формы.ТипФормы(Форма) <> "ФормаЭлемента" Тогда Возврат Неопределено КонецЕсли;
		
		ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(Форма, Форма.Объект);
	КонецФункции
	
	Функция ФормаПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
		Если НЕ ВыполнятьМеханизм(Форма) Тогда Возврат Неопределено КонецЕсли;
		
		ПодключаемыеКоманды.ПриСозданииНаСервере(Форма);
		
		Если А1Э_Формы.ТипФормы(Форма) = "ФормаСписка" Тогда
			А1Э_УниверсальнаяФорма.ДобавитьОбработчикЭлементаФормы(Форма, Форма.Элементы.Список, "ПриАктивизацииСтроки", "А1БСП_ПодключаемыеКоманды.ПриАктивизацииСтроки");
		КонецЕсли;
		Для Каждого Команда Из Форма.Команды Цикл
			Если Команда.Действие <> "Подключаемый_ВыполнитьКоманду" Тогда Продолжить; КонецЕсли;
			А1Э_УниверсальнаяФорма.ЗаменитьДействиеКомандыФормы(Форма, Команда, "А1БСП_ПодключаемыеКоманды.ВыполнитьКоманду");	
		КонецЦикла;
		
	КонецФункции
	
#КонецЕсли
#Если Клиент Тогда
	
	Функция ФормаПриОткрытии(Форма, Отказ) Экспорт
		Если НЕ ВыполнятьМеханизм(Форма) Тогда Возврат Неопределено; КонецЕсли;
		
		НачатьОбновлениеКоманд(Форма);
	КонецФункции
	
#КонецЕсли
#КонецОбласти 

#Область Команды
#Если Клиент Тогда
	
	Функция ВыполнитьКоманду(Форма, Команда) Экспорт
		ПодключаемыеКомандыКлиент.ВыполнитьКоманду(Форма, Команда, ОбъектИлиТаблицаФормы(Форма));
	КонецФункции 
	
	Функция ПриАктивизацииСтроки(Форма, Элемент) Экспорт 
		НачатьОбновлениеКоманд(Форма);
	КонецФункции 
	
	Функция НачатьОбновлениеКоманд(Форма)
		А1Э_УниверсальнаяФорма.ПодключитьУниверсальныйОбработчикОжидания(Форма, "А1БСП_ПодключаемыеКоманды.ОбновитьКоманды", 0.2, Истина); 
	КонецФункции 
#КонецЕсли
#КонецОбласти 

#Область Служебно

Функция ВыполнятьМеханизм(Форма)
	ТипФормы = А1Э_Формы.ТипФормы(Форма);
	Возврат ТипФормы = "ФормаЭлемента" Или ТипФормы = "ФормаСписка";
КонецФункции

Функция ОбъектИлиТаблицаФормы(Форма)
	Если А1Э_Формы.ТипФормы(Форма) = "ФормаЭлемента" Тогда
		Возврат Форма.Объект;
	ИначеЕсли А1Э_Формы.ТипФормы(Форма) = "ФормаСписка" Тогда
		Возврат Форма.Элементы.Список;
	Иначе
		А1Э_Служебный.СлужебноеИсключение("Неверный тип формы!");
	КонецЕсли;
	
КонецФункции

Функция ОбновитьКоманды(Форма) Экспорт
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(Форма, ОбъектИлиТаблицаФормы(Форма));
КонецФункции 

#КонецОбласти 

#Если НЕ Клиент Тогда
	
	Функция ПриОпределенииОбъектовСКомандамиПечати(СписокОбъектов) Экспорт 
		ОбъектыМеханизма = А1Э_Механизмы.ВсеМеханизмыСОбъектами()["А1БСП_ПодключаемыеКоманды"];
		Если ОбъектыМеханизма = Неопределено Тогда Возврат Неопределено; КонецЕсли;
		
		Для Каждого Пара Из ОбъектыМеханизма Цикл
			СписокОбъектов.Добавить(А1Э_Метаданные.МенеджерОбъекта(Пара.Ключ));
		КонецЦикла;
	КонецФункции 
	
#КонецЕсли

