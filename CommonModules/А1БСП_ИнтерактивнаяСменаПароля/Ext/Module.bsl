Функция НастройкиМеханизма() Экспорт
	Настройки = А1Э_Механизмы.НовыйНастройкиМеханизма();
	
	Настройки.Обработчики.Вставить("ПриНачалеРаботыСистемы", Истина);
	
	Возврат Настройки;
КонецФункции

#Если Клиент Тогда
	Функция ПриНачалеРаботыСистемы() Экспорт
		Если НЕ А1Э_ОбщееСервер.РезультатФункции(ИмяМодуля() + ".НужноСменитьПарольПриВходе") Тогда Возврат Неопределено; КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ПриВходеВПрограмму", Истина);

		ОткрытьФорму("ОбщаяФорма.СменаПароля", ПараметрыФормы, , , , , Новый ОписаниеОповещения("ПриЗакрытииФормыСменыПароля", ЭтотОбъект));
	КонецФункции
	
	Функция ПриЗакрытииФормыСменыПароля(Результат, Контекст) Экспорт
		Если Результат = Неопределено Тогда
			ЗавершитьРаботуСистемы(Ложь);
		КонецЕсли;
	КонецФункции 
#КонецЕсли
#Если НЕ Клиент Тогда
	
	Функция НужноСменитьПарольПриВходе() Экспорт
		УстановитьПривилегированныйРежим(Истина);
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сведения.ПотребоватьСменуПароляПриВходе КАК ПотребоватьСменуПароляПриВходе
		|ИЗ
		|	РегистрСведений.СведенияОПользователях КАК Сведения
		|ГДЕ
		|	Сведения.Пользователь = &ТекущийПользователь";
		Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
		Возврат А1Э_Запросы.ПервыйРезультат(Запрос, , Ложь);
	КонецФункции 
#КонецЕсли
 
Функция ИмяМодуля() Экспорт
	Возврат "А1БСП_ИнтерактивнаяСменаПароля";
КонецФункции  