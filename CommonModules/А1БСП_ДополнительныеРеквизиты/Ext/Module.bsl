#Если НЕ Клиент Тогда
	
	// Возвращает элемент формы, соответствующий переданному имени доп. реквизита.
	// Если элемент формы отсутствует, возвращает Неопределено.
	//
	// Параметры:
	//  Форма						 - ФормаКлиентскогоПриложения - 
	//  ИмяДополнительногоРеквизита	 - Строка - 
	// 
	// Возвращаемое значение:
	//   - ЭлементФормы,Неопределено
	//
	Функция ЭлементФормы(Форма, ИмяДополнительногоРеквизита) Экспорт 
		ИмяЭлементаФормы = ИмяЭлементаФормы(Форма, ИмяДополнительногоРеквизита);
		Если ИмяЭлементаФормы = Неопределено Тогда Возврат Неопределено; КонецЕсли;
		Возврат Форма.Элементы[ИмяЭлементаФормы];	
	КонецФункции
	
	// Возвращает имя элемента формы, соответствующего переданному имени доп. реквизита.
	//
	// Параметры:
	//  Форма						 - ФормаКлиентскогоПриложения - 
	//  ИмяДополнительногоРеквизита	 - Строка - 
	// 
	// Возвращаемое значение:
	//   - ЭлементФормы,Неопределено
	//
	Функция ИмяЭлементаФормы(Форма, ИмяДополнительногоРеквизита) Экспорт
		ОписаниеДопРеквизитов = Форма.Свойства_ОписаниеДополнительныхРеквизитов;
		Строки = ОписаниеДопРеквизитов.НайтиСтроки(А1Э_Структуры.Создать(
		"Свойство", А1Э_ДопРеквизиты.Свойство(ИмяДополнительногоРеквизита),
		));
		Если Строки.Количество() = 0 Тогда Возврат Неопределено; КонецЕсли;
		Возврат Строки[0].ИмяРеквизитаЗначение;	
	КонецФункции 
	
	// Перемещает поле, ответственное за дополнительный реквизит.
	//
	// Параметры:
	//  Форма			 - ФормаКлиентскогоПриложения - 
	//  Имя				 - Строка - имя дополнительного реквизита. 
	//  РодительЭлемента - Строка,ЭлементФормы,ФормаКлиентскогоПриложения - 
	//  ПередЭлементом	 - Строка,ЭлементФормы,Неопределено	 - 
	// 
	// Возвращаемое значение:
	//   - 
	//
	Функция Переместить(Форма, Имя, Знач РодительЭлемента, Знач ПередЭлементом = Неопределено) Экспорт
		ЭлементДопРеквизита = ЭлементФормы(Форма, Имя);
		Если ЭлементДопРеквизита = Неопределено Тогда Возврат Неопределено; КонецЕсли;
		
		Форма.Элементы.Переместить(ЭлементДопРеквизита, А1Э_Формы.ЭлементФормы(Форма, РодительЭлемента), А1Э_Формы.ЭлементФормы(Форма, ПередЭлементом));
	КонецФункции 
	
	// Устанавливает значение поля формы, соответствующего полю в интерфейсе.
	//
	// Параметры:
	//  Форма	 					     - ФормаКлиентскогоПриложения - 
	//  ИмяДополнительногоРеквизита		 - Строка - 
	//  Значение 						 - Произвольный	 - 
	// 
	// Возвращаемое значение:
	//   - 
	//
	Функция УстановитьНаФорме(Форма, ИмяДополнительногоРеквизита, Значение) Экспорт
		Форма[ИмяЭлементаФормы(Форма, ИмяДополнительногоРеквизита)] = Значение;
	КонецФункции
	
	// В схеме компоновки данных заполняет списки значений полей доп. реквизитов и сведений соответствующими значениями.
	// Предназначается для использования при инициализации отчетов.
	//
	// Параметры:
	//  СхемаКомпоновкиДанных	 - СхемаКомпоновкиДанных - 
	//  СтруктураДопРеквизитов	 - Структура<[ИмяПоля]:[ИмяНабораДополнительныхРеквизитов]> - 
	// 
	// Возвращаемое значение:
	//   - 
	//
	Функция УстановитьДоступныеЗначенияПолей(СхемаКомпоновкиДанных, СтруктураДопРеквизитов) Экспорт
		#Если Сервер И НЕ Сервер Тогда
			СхемаКомпоновкиДанных = Новый СхемаКомпоновкиДанных;		
		#КонецЕсли
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Значения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ЗначенияСвойствОбъектов КАК Значения
		|ГДЕ
		|	Значения.Владелец.Имя = &ИмяДопРеквизита"
		);
		Для Каждого Пара Из СтруктураДопРеквизитов Цикл
			Запрос.УстановитьПараметр("ИмяДопРеквизита", Пара.Значение);
			ДоступныеЗначения = Новый СписокЗначений;
			ДоступныеЗначения.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
			Для Каждого НаборДанных Из СхемаКомпоновкиДанных.НаборыДанных Цикл
				Поле = НаборДанных.Поля.Найти(Пара.Ключ);
				Если Поле = Неопределено Тогда Продолжить; КонецЕсли;
				Поле.УстановитьДоступныеЗначения(ДоступныеЗначения);
			КонецЦикла;
		КонецЦикла;		
	КонецФункции
	
#КонецЕсли

#Область Механизм

// Механизм предназначен для подключения дополнительных реквизитов (стандартных из БСП) к объектам метаданных, добавленным в расширениях.
//  Для работы механизмы необходимо создать реквизит с именем "<Префикс>_Владелец" (туда будет записано имя метаданных владельца доп. реквизитов).
//	После первого запуска рекомендуется выполнить тестирование и исправление механизмов, которое автоматически создаст нужные наборы.
//	В данной версии для работы характеристик нужно вручную создать реквизит "<Префикс>_Владелец" в табличной части документа и подключить его к характеристикам. 
// 
// Возвращаемое значение:
//   - 
//
Функция НастройкиМеханизма() Экспорт
	Настройки = А1Э_Механизмы.НовыйНастройкиМеханизма();
	
	Настройки.Обработчики.Вставить("А1Э_ПриПоискеОшибок", Истина);
	Настройки.Обработчики.Вставить("А1Э_ПриИсправленииОшибок", Истина);
	
	Настройки.Обработчики.Вставить("ФормаЭлементаПриСозданииНаСервере", Истина);
	Настройки.Обработчики.Вставить("ФормаЭлементаПриЧтенииНаСервере", Истина);
	Настройки.Обработчики.Вставить("ФормаЭлементаОбработкаЗаполненияНаСервере", Истина);
	Настройки.Обработчики.Вставить("ФормаЭлементаПередЗаписьюНаСервере", Истина);
	
	Настройки.Обработчики.Вставить("ФормаЭлементаПриОткрытии", Истина);
	Настройки.Обработчики.Вставить("ФормаЭлементаОбработкаОповещения", Истина);
	
	Настройки.ПорядокВыполнения = 1000;
	
	Возврат Настройки;
КонецФункции

#Если НЕ Клиент Тогда
	
	Функция А1Э_ПриПоискеОшибок(Ошибки) Экспорт
		ТаблицаПрефиксов = ТаблицаОбъектовИПрефиксов();
		
		Префиксы = А1Э_ТаблицыЗначений.РазбитьВТаблицыПоКолонке(ТаблицаПрефиксов, "Префикс");
		Реквизиты = Метаданные.Справочники.НаборыДополнительныхРеквизитовИСведений.Реквизиты;
		ПрефиксыБезРеквизита = Новый Массив;
		Для Каждого Пара Из Префиксы Цикл
			Префикс = Пара.Ключ;
			Если Реквизиты.Найти(Префикс + "_Владелец") = Неопределено Тогда
				А1Э_Механизмы.ДобавитьОписаниеОшибки(Ошибки, "ОтсутствуетРеквизит", , , "Отсутствует реквизит справочника <НаборыДополнительныхРеквизитовИСведений>", , 
				А1Э_Структуры.Создать(
				"Префикс", Префикс,
				));
				ПрефиксыБезРеквизита.Добавить(Префикс);
			КонецЕсли;
		КонецЦикла;
		А1Э_Структуры.УдалитьКлючи(Префиксы, ПрефиксыБезРеквизита);
		
		Для Каждого Пара Из Префиксы Цикл
			Префикс = Пара.Ключ;
			Запрос = Новый Запрос(ТекстЗапросаОтсутствующихНаборов(Префикс));
			Запрос.УстановитьПараметр("ТаблицаПрефикса", Пара.Значение);
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если НЕ ЗначениеЗаполнено(Выборка.СсылкаНабора) Тогда
					А1Э_Механизмы.ДобавитьОписаниеОшибки(Ошибки, "ОтсутствуетНаборРеквизитов", Выборка.Объект, , "Отсутствует элемент справочника <НаборыДополнительныхРеквизитовИСведений>", Истина, 
					А1Э_Структуры.Создать(
					"Префикс", Префикс,
					"ПредставлениеНабора", Выборка.ПредставлениеНабора,
					));
				Иначе
					А1Э_Механизмы.ДобавитьОписаниеОшибки(Ошибки, "НеверноеИмяНабораРеквизитов", Выборка.Объект, Выборка.СсылкаНабора, "Отсутствует элемент справочника <НаборыДополнительныхРеквизитовИСведений>", Истина, 
					А1Э_Структуры.Создать(
					"Префикс", Префикс,
					"ПредставлениеНабора", Выборка.ПредставлениеНабора,
					));
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	КонецФункции
	
	Функция ТаблицаОбъектовИПрефиксов()
		ТаблицаПрефиксов = Новый ТаблицаЗначений;
		ТаблицаПрефиксов.Колонки.Добавить("Префикс", А1Э_Строки.ОписаниеТипа(100));
		ТаблицаПрефиксов.Колонки.Добавить("Объект", А1Э_Строки.ОписаниеТипа(100));
		ТаблицаПрефиксов.Колонки.Добавить("ПредставлениеНабора", А1Э_Строки.ОписаниеТипа(100));
		
		ОбъектыМеханизма = А1Э_Механизмы.ОбъектыМеханизма("А1БСП_ДополнительныеРеквизиты");
		Для Каждого Пара Из ОбъектыМеханизма Цикл
			Объект = Пара.Ключ;
			ОбъектМетаданных = А1Э_Метаданные.ОбъектМетаданных(Объект);
			Контекст = Пара.Значение;
			Строка = ТаблицаПрефиксов.Добавить();
			Строка.Объект = Объект;
			Строка.Префикс = Префикс(ОбъектМетаданных);
			
			Строка.ПредставлениеНабора = А1Э_Структуры.ЗначениеСвойства(Контекст, "ПредставлениеНабора");
			Если НЕ ЗначениеЗаполнено(Строка.ПредставлениеНабора) Тогда
				Строка.ПредставлениеНабора = А1Э_Метаданные.ПредставлениеНескольких(ОбъектМетаданных);
			КонецЕсли;
		КонецЦикла;
		Возврат ТаблицаПрефиксов;
	КонецФункции
	
	Функция А1Э_ПриИсправленииОшибок(Ошибки) Экспорт
		Для Каждого Ошибка Из Ошибки Цикл
			Если Ошибка.Имя = "ОтсутствуетНаборРеквизитов" Или Ошибка.Имя = "НеверноеИмяНабораРеквизитов" Тогда
				Если Ошибка.Имя = "ОтсутствуетНаборРеквизитов" Тогда
					НаборРеквизитов = Справочники.НаборыДополнительныхРеквизитовИСведений.СоздатьЭлемент();
					НаборРеквизитов[ИмяРеквизита(Ошибка.Контекст.Префикс)] = Ошибка.ОбъектМетаданных;
				Иначе
					НаборРеквизитов = Ошибка.Ссылка.ПолучитьОбъект();	
				КонецЕсли;
				НаборРеквизитов.Используется = Истина;
				НаборРеквизитов.Наименование = Ошибка.Контекст.ПредставлениеНабора;
				НаборРеквизитов.Записать();
			КонецЕсли;
		КонецЦикла;
		
	КонецФункции 
	
	Функция ФормаЭлементаПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
		Если НЕ Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьДополнительныеРеквизитыИСведения") Тогда Возврат Неопределено КонецЕсли;
		
		Контекст = А1Э_Механизмы.КонтекстМеханизма(Форма, "А1БСП_ДополнительныеРеквизиты");
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", А1Э_Общее.ЗначениеСвойства(Контекст, "ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты"));
		УправлениеСвойствами.ПриСозданииНаСервере(Форма, ДополнительныеПараметры);
	КонецФункции
	
	Функция ФормаЭлементаПриЧтенииНаСервере(Форма, ТекущийОбъект) Экспорт 
		Если НЕ Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьДополнительныеРеквизитыИСведения") Тогда Возврат Неопределено КонецЕсли;
		
		Контекст = А1Э_Механизмы.КонтекстМеханизма(Форма, "А1БСП_ДополнительныеРеквизиты");
		Если НЕ А1Э_Структуры.Свойство(Контекст, "ИмяЭлементаДляРазмещения") Тогда Возврат Неопределено; КонецЕсли;
		
		УправлениеСвойствами.ПриЧтенииНаСервере(Форма, ТекущийОбъект);
	КонецФункции
	
	Функция ФормаЭлементаОбработкаЗаполненияНаСервере(Форма, Отказ, ПроверяемыеРеквизиты) Экспорт
		Если НЕ Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьДополнительныеРеквизитыИСведения") Тогда Возврат Неопределено КонецЕсли;
		
		Контекст = А1Э_Механизмы.КонтекстМеханизма(Форма, "А1БСП_ДополнительныеРеквизиты");
		Если НЕ А1Э_Структуры.Свойство(Контекст, "ИмяЭлементаДляРазмещения") Тогда Возврат Неопределено; КонецЕсли;
		
		УправлениеСвойствами.ОбработкаПроверкиЗаполнения(Форма, Отказ, ПроверяемыеРеквизиты);
	КонецФункции
	
	Функция ФормаЭлементаПередЗаписьюНаСервере(Форма, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт
		Если НЕ Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьДополнительныеРеквизитыИСведения") Тогда Возврат Неопределено КонецЕсли;
		
		Контекст = А1Э_Механизмы.КонтекстМеханизма(Форма, "А1БСП_ДополнительныеРеквизиты");
		Если НЕ А1Э_Структуры.Свойство(Контекст, "ИмяЭлементаДляРазмещения") Тогда Возврат Неопределено; КонецЕсли;
		
		УправлениеСвойствами.ПередЗаписьюНаСервере(Форма, ТекущийОбъект);
	КонецФункции 
	
	Функция ФормаЭлементаОбработкаОповещенияСервер(Форма, ИмяСобытия, Параметр, Источник) Экспорт
		УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(Форма);
	КонецФункции 
	
#КонецЕсли
#Если Клиент Тогда
	
	Функция ФормаЭлементаПриОткрытии(Форма, Отказ) Экспорт
		Если НЕ Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьДополнительныеРеквизитыИСведения") Тогда Возврат Неопределено КонецЕсли;
		
		Контекст = А1Э_Механизмы.КонтекстМеханизма(Форма, "А1БСП_ДополнительныеРеквизиты");
		Если НЕ А1Э_Структуры.Свойство(Контекст, "ИмяЭлементаДляРазмещения") Тогда Возврат Неопределено; КонецЕсли;
		
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(Форма);
	КонецФункции
	
	Функция ФормаЭлементаОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
		Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(Форма, ИмяСобытия, Параметр) Тогда
			А1Э_Механизмы.ЗапланироватьСерверныйОбработчик(Форма, ИмяМодуля() + ".ФормаЭлементаОбработкаОповещенияСервер")
		КонецЕсли;
	КонецФункции 
	
	Функция Подключаемый_СвойстваВыполнитьКоманду(Форма, ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено) Экспорт
		УправлениеСвойствамиКлиент.ВыполнитьКоманду(Форма, ЭлементИлиКоманда, СтандартнаяОбработка);
	КонецФункции
	
	Функция Подключаемый_ПриИзмененииДополнительногоРеквизита(Форма, Элемент) Экспорт
		УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(Форма);
	КонецФункции
	
#КонецЕсли

#КонецОбласти 

#Область Общее
#Если НЕ Клиент Тогда
	// Возвращает префикс, используемый механизмом дополнительных реквизитов.
	//
	// Параметры:
	//  Объект	 - 	 - 
	// 
	// Возвращаемое значение:
	//   - 
	//
	Функция Префикс(ОбъектМетаданных) Экспорт
		Если НЕ А1Э_Механизмы.Подключен(ОбъектМетаданных, ИмяМодуля()) Тогда Возврат Неопределено; КонецЕсли;
		Контекст = А1Э_Механизмы.КонтекстМеханизма(ОбъектМетаданных, ИмяМодуля());
		
		Префикс = А1Э_Структуры.ЗначениеСвойства(Контекст, "Префикс");
		Если НЕ ЗначениеЗаполнено(Префикс) Тогда 
			Префикс = А1Э_Строки.Перед(ОбъектМетаданных.Имя, "_"); 
		КонецЕсли;
		Возврат Префикс;
	КонецФункции 
	
#КонецЕсли
#КонецОбласти

Функция ТекстЗапросаОтсутствующихНаборов(Префикс) Экспорт
	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаПрефикса.Объект КАК Объект,
	|	ТаблицаПрефикса.ПредставлениеНабора КАК ПредставлениеНабора
	|ПОМЕСТИТЬ ТаблицаПрефикса
	|ИЗ
	|	&ТаблицаПрефикса КАК ТаблицаПрефикса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПрефикса.Объект КАК Объект,
	|	ТаблицаПрефикса.ПредставлениеНабора КАК ПредставлениеНабора,
	|	Наборы.Ссылка КАК СсылкаНабора,
	|	Наборы.Наименование КАК НаименованиеНабора
	|ИЗ
	|	ТаблицаПрефикса КАК ТаблицаПрефикса
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений КАК Наборы
	|		ПО (ТаблицаПрефикса.Объект = &Наборы_РеквизитВладелец)
	|			И (Наборы.Используется)
	|ГДЕ
	|	(Наборы.Ссылка ЕСТЬ NULL
	|			ИЛИ ТаблицаПрефикса.ПредставлениеНабора <> Наборы.Наименование)";
	А1Э_Строки.Подставить(Текст, "&Наборы_РеквизитВладелец", "Наборы." + ИмяРеквизита(Префикс));
	Возврат Текст;
КонецФункции 

Функция ИмяРеквизита(Префикс) Экспорт
	Возврат Префикс + "_Владелец";	
КонецФункции

Функция ИмяМодуля() Экспорт
	Возврат "А1БСП_ДополнительныеРеквизиты";
КонецФункции 